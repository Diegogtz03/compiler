/* Lexico - Palabras reservadas */

program : 'p' 'r' 'o' 'g' 'r' 'a' 'm';
main : 'm' 'a' 'i' 'n';
var: 'v' 'a' 'r';
end: 'e' 'n' 'd';
int_rw: 'i' 'n' 't';
float_rw: 'f' 'l' 'o' 'a' 't';
void: 'v' 'o' 'i' 'd';
while: 'w' 'h' 'i' 'l' 'e';
do: 'd' 'o';
print: 'p' 'r' 'i' 'n' 't';
if: 'i' 'f';
else: 'e' 'l' 's' 'e';

/* Lexico - Simbolos */

semicolon: ';';
two_dots: ':';
comma: ',';
curly_open: '{';
curly_close: '}';
parenthesis_open: '(';
parenthesis_close: ')';
bracket_open: '[';
bracket_close: ']';
equal: '=';
plus: '+';
minus: '-';
greater_than: '>';
less_than: '<';
not_equal: '!' '=';
mult: '*';
div: '/';

/* Lexico - Constantes */

id : 'a'-'z' {'a'-'z'} { ('_' | 'A'-'Z') ('a'-'z' | 'A'-'Z' | '0'-'9') {'a'-'z' | 'A'-'Z' | '0'-'9'} } ;
myint : '0'-'9' {'0'-'9'} ;
myfloat : '0'-'9' {'0'-'9'} '.' '0'-'9' {'0'-'9'} ;
string : '"' {'a'-'z' | 'A'-'Z'} '"' ;

!whitespace : ' ' | '\t' | '\n' | '\r' ;

/* ------------------------ */
/* Sintaxis */

Start : Programa ;

Program_Point : program << ast.TestAST(), nil >> ;

Programa : Program_Point id semicolon Vars Programa_PR main Body end
  | Program_Point id semicolon Programa_PR main Body end;

Programa_PR : Funcs Programa_PR
  | empty ;

Vars : var id Vars_PR two_dots Type semicolon Vars_PR_PR;

Vars_PR : comma id Vars_PR
  | empty ;

Vars_PR_PR : id Vars_PR two_dots Type semicolon Vars_PR_PR
  | empty ;

Type : int_rw | float_rw ;

Body : curly_open Body_PR curly_close;
Body_PR : Statement Body_PR
  | empty ;

Statement : Assign 
  | Condition
  | Cycle
  | F_call
  | Print;

Print : print parenthesis_open Expr Print_PR parenthesis_close semicolon
  | print parenthesis_open string Print_PR parenthesis_close semicolon;

Print_PR : comma Expr Print_PR
  | comma string Print_PR
  | empty ;

Assign : id equal Exp semicolon;

Cycle : while parenthesis_open Expr parenthesis_close do Body semicolon;

Condition : if parenthesis_open Expr parenthesis_close Body semicolon
  | if parenthesis_open Expr parenthesis_close Body else Body semicolon;

Cte : myint | myfloat;

Expr : Exp
  | Exp greater_than Exp
  | Exp less_than Exp
  | Exp not_equal Exp;

Exp : Term
  | Term plus Exp
  | Term minus Exp;

Term : Fact
  | Fact mult Term
  | Fact div Term;

Fact : parenthesis_open Expr parenthesis_close
  | plus id
  | minus id
  | Cte
  | id
  | plus Cte
  | minus Cte;

Funcs : void id parenthesis_open parenthesis_close bracket_open Body bracket_close semicolon
  | void id parenthesis_open parenthesis_close bracket_open Vars Body bracket_close semicolon
  | void id parenthesis_open id two_dots Type Funcs_PR parenthesis_close bracket_open Body bracket_close semicolon
  | void id parenthesis_open id two_dots Type Funcs_PR parenthesis_close bracket_open Vars Body bracket_close semicolon;

Funcs_PR : comma id two_dots Type Funcs_PR
  | empty ;

F_call : id parenthesis_open parenthesis_close semicolon
  | id parenthesis_open Expr F_call_PR parenthesis_close semicolon;

F_call_PR : comma Expr F_call_PR 
  | empty ;